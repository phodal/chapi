apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.artifactory'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/phodal/chapi")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
//        gpr(MavenPublication) {
//            from(components.java)
//        }
        mavenJava(MavenPublication) {
            from components.java
            version project.version
            pom {
                afterEvaluate {
                    name = project.description
                    description = "Chapi is A common language meta information convertor, convert different languages to same meta-data model"
                }
                url.set("https://github.com/phodal/chapi.git")
                organization {
                    name = "Phodal"
                    url = "https://www.phodal.com/"
                }
                licenses {
                    license {
                        name = "MPL 2.0"
                        url = "https://github.com/phodal/chapi/blob/master/LICENSE"
                        distribution = "repo"
                    }
                }
                scm {
                    url = "https://github.com/phodal/chapi"
                    connection = "scm:git:git://github.com/phodal/chapi"
                    developerConnection = "scm:git:git://github.com/phodal/chapi"
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_API_KEY')
    publish = true
    configurations = ['archives']
    publications = ['mavenJava']
    pkg {
        repo = "chapi"
        name = "chapi"
        userOrg = "phodal"
        websiteUrl = "https://www.phodal.com"
        githubRepo = "phodal/chapi"
        licenses = ['MPL-2.0']
        vcsUrl = "https://github.com/phodal/chapi"
        description = "Chapi is A common language meta information convertor, convert different languages to same meta-data model"
        desc = description
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = project.version
            desc = project.description
            released = new Date()
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.mavenJava)
}
